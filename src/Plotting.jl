using PyPlot
import PyCall
export plot, savefig, conformal_grid


const mygreen = [151/255,180/255,118/255];
const mygreen2 = [113/255,161/255,103/255];
const myblue = [74/255,144/255,226/255];

PlotColor = Union{Vector{Float64},String,Symbol}

struct PlotStyle

  bodycolor :: PlotColor
  xicolor   :: PlotColor
  etacolor  :: PlotColor
  markercolor :: PlotColor
  linewidth :: Int64
  pointsize :: Int64

end

function PlotStyle()
  PyCall.PyDict(matplotlib["rcParams"])["font.family"] = "STIXGeneral";
  PyCall.PyDict(matplotlib["rcParams"])["mathtext.fontset"] = "stix";
  PyCall.PyDict(matplotlib["rcParams"])["font.size"] = 10;

  bodycolor = mygreen
  xicolor = mygreen2
  etacolor = myblue
  markercolor = "r"
  linewidth = 1
  pointsize = 3

  PlotStyle(bodycolor,xicolor,etacolor,markercolor,linewidth,pointsize)

end

"""
    plot(p::Polygon)

Plots the polygon `p`.

# Example

```jldoctest
julia> p = Polygon([-1.0,0.2,1.0,-1.0],[-1.0,-1.0,0.5,1.0]);

julia> plot(p);
```
"""
function plot(p::Polygon)
  ps = PlotStyle()
  w = [p.vert;p.vert[1]]
  pf = PyPlot.axes()
  pf[:fill](real(w),imag(w),facecolor=ps.bodycolor);
  pf[:plot](real(w),imag(w),color=ps.bodycolor);
  pf[:axis]("scaled")
  nothing
end

"""
    conformal_grid(m::ConformalMap)

Plots the grid lines generated by the exterior mapping `m`.

# Example

```jldoctest paramtest
julia> p = Polygon([-1.0,0.2,1.0,-1.0],[-1.0,-1.0,0.5,1.0]);

julia> m = ExteriorMap(p);

julia> conformal_grid(m);
```
"""
function conformal_grid(m::SchwarzChristoffel.ConformalMap)

  ps = PlotStyle()

  xmaxc = 4.0
  rmax = sqrt(2)*xmaxc

  fig, (ax1,ax2) = PyPlot.subplots(1,2)
  ax1[:axis]("scaled")
  ax1[:axis]([-xmaxc,xmaxc,-xmaxc,xmaxc])
  ax1[:set_xticks](ceil(-xmaxc/2)*2:2:floor(xmaxc/2)*2)
  ax1[:set_yticks](ceil(-xmaxc/2)*2:2:floor(xmaxc/2)*2)

  xmaxp = xmaxc*abs(m.ccoeff[1])
  dxp = round(xmaxp)*0.5
  ax2[:axis]("scaled")
  ax2[:axis]([-xmaxp,xmaxp,-xmaxp,xmaxp])
  ax2[:set_xticks](ceil(-xmaxp/dxp)*dxp:dxp:floor(xmaxp/dxp)*dxp)
  ax2[:set_yticks](ceil(-xmaxp/dxp)*dxp:dxp:floor(xmaxp/dxp)*dxp)

  nθ = 20
  dθ = 2π/nθ
  θg = 0
  nrg = 100
  rg = linspace(1,rmax,nrg)
  for jθ in 1:nθ
      ζg = collect(rg*exp(im*θg))
      zg = m(ζg)

       ax1[:plot](real(ζg),imag(ζg),linewidth=ps.linewidth,color=ps.xicolor)
       ax2[:plot](real(zg),imag(zg),linewidth=ps.linewidth,color=ps.xicolor)

       θg += dθ
   end

   nr = 10
   dr = (rmax-1)/nr
   rg = 1

   nθg = 200
   θg = linspace(0,2π,nθg)
   for jr in 1:nr
       ζg = collect(rg*exp.(im*θg));
       zg = m(ζg)

       ax1[:plot](real(ζg),imag(ζg),linewidth=ps.linewidth,color=ps.etacolor)
       ax2[:plot](real(zg),imag(zg),linewidth=ps.linewidth,color=ps.etacolor)

       rg += dr
   end

end
